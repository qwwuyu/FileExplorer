apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion configs.compileSdkVersion

    defaultConfig {
        applicationId "com.qwwuyu.file"
        minSdkVersion configs.minSdkVersion
        targetSdkVersion configs.targetSdkVersion
        versionCode configs.versionCode
        versionName configs.versionName

        manifestPlaceholders = [provider: "${android.defaultConfig.applicationId}.provider"]
        buildConfigField "String", "PROVIDER", "\"${android.defaultConfig.applicationId}.provider\""
    }

    if (signInfo.isSign) {
        signingConfigs {
            sign {
                storeFile file(signInfo.storeFile)
                storePassword signInfo.storePassword
                keyAlias signInfo.keyAlias
                keyPassword signInfo.keyPassword
            }
        }
    }

    buildTypes {
        debug {
            if (signInfo.isSign) signingConfig signingConfigs.sign
            versionNameSuffix ".debug"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = false

            buildConfigField "boolean", "TEST", "$configs.test"
            buildConfigField "String", "REDIRECT_URL", "\"$configs.redirect\""
            buildConfigField "String", "REDIRECT_KEY", "\"$configs.redirectKey\""
        }

        release {
            if (signInfo.isSign) signingConfig signingConfigs.sign
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'

            buildConfigField "boolean", "TEST", "false"
            buildConfigField "String", "REDIRECT_URL", "\"\""
            buildConfigField "String", "REDIRECT_KEY", "\"\""
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "FileManage${variant.mergedFlavor.versionName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.annotation:annotation:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.room:room-runtime:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    implementation "com.google.code.gson:gson:2.8.5"
}
