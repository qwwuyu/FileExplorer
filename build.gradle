buildscript {
    ext.kotlin_version = '1.3.71'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    def stream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(stream)
    stream.close()
}
ext.getPropertyByName = { key, defValue ->
    def value = properties.getProperty(key)
    if (value == null) value = System.getenv(key)
    return value != null ? value : defValue
}
ext {
    int major = 2, minor = 0, revision = 0
    configs = [
            compileSdkVersion: 29,
            minSdkVersion    : 19,
            targetSdkVersion : 28,
            versionCode      : major * 10000 + minor * 100 + revision,
            versionName      : major + "." + minor + "." + revision,
            currentDate      : new Date().format('yyMMdd-HH'),
            test             : getPropertyByName("APP_TEST_ENABLE", "false"),
    ]

    signInfo = [
            storeFile    : getPropertyByName("STORE_FILE", null),
            storePassword: getPropertyByName("STORE_PASSWORD", null),
            keyAlias     : getPropertyByName("KEY_ALIAS", null),
            keyPassword  : getPropertyByName("KEY_PASSWORD", null),
    ]
    signInfo.isSign = signInfo.storeFile != null && signInfo.keyAlias != null

    versions = [
            junit           : "junit:junit:4.12",
            appcompat       : "androidx.appcompat:appcompat:1.1.0",
            annotation      : "androidx.annotation:annotation:1.1.0",
            constraintlayout: "androidx.constraintlayout:constraintlayout:1.1.3",

            rxjava2         : "2.2.7",
            rxandroid2      : "2.1.1",
    ]
}
